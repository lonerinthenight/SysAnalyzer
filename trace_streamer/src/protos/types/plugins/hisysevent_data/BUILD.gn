# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../protos.gni")

hisysevent_data_sources = [
  "./hisysevent_plugin_config.proto",
  "./hisysevent_plugin_result.proto",
]

#######################################################
proto_out_dir = "$root_gen_dir/cpp/" + rebase_path(".", "//")
proto_rel_out_dir = rebase_path(proto_out_dir, root_build_dir)

hisysevent_data_codegen = []
hisysevent_data_codegen_reader = []
hisysevent_data_codegen_all = []

foreach(proto, hisysevent_data_sources) {
  name = get_path_info(proto, "name")
  hisysevent_data_codegen += [
    "$proto_out_dir/$name.pb.h",
    "$proto_out_dir/$name.pb.cc",
  ]
  hisysevent_data_codegen_reader += [ "$proto_out_dir/$name.pbreader.h" ]
}

hisysevent_data_codegen_all += hisysevent_data_codegen
hisysevent_data_codegen_all += hisysevent_data_codegen_reader

config("hisysevent_include_config") {
  include_dirs = [ "$proto_out_dir" ]
}

#######################################################
action("ts_hisysevent_data_cpp_gen") {
  script = "${OHOS_TRACE_STREAMER_DIR_PROTOC}"
  sources = hisysevent_data_sources
  outputs = hisysevent_data_codegen_all
  args = [
    "$libc_dir_proto",
    "$root_output_dir_proto",
    "$proto_rel_out_dir",
    "--cpp_out",
    "$proto_rel_out_dir",
    "--proto_path",
    rebase_path(".", root_build_dir),
  ]
  args += rebase_path(sources, root_build_dir)
  if (!use_wasm && !is_test && !is_fuzz) {
    deps = [
      "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protoc(${host_toolchain})",
      "${SRC}/proto_reader/protoc_plugin:protoreader_plugin(${host_toolchain})",
    ]
  }
}

ohos_source_set("ts_hisysevent_data_cpp") {
  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  part_name = "${OHOS_PROFILER_PART_NAME}"
  deps = [ ":ts_hisysevent_data_cpp_gen" ]
  public_deps = [
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf_lite_static",
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf_static",
  ]
  include_dirs = [ "$proto_out_dir" ]
  public_configs = [ ":hisysevent_include_config" ]
  sources = hisysevent_data_codegen
}

ohos_source_set("hisysevent_data_reader") {
  subsystem_name = "${OHOS_PROFILER_SUBSYS_NAME}"
  part_name = "${OHOS_PROFILER_PART_NAME}"
  deps = [ ":ts_hisysevent_data_cpp_gen" ]
  include_dirs = [ "$proto_out_dir" ]
  public_configs = [ ":hisysevent_include_config" ]
  sources = hisysevent_data_codegen_reader
}
